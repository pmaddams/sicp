#lang racket/base

(provide (all-defined-out))

(define (make-env builtins)
  (let ((vars (map car builtins))
        (vals (map cdr builtins)))
    (cons (make-hash)
          (subst vars vals '()))))

(define (subst vars vals env)
  (if (= (length vars) (length vals))
      (let ((env* (cons (make-hash) env)))
        (for ((var (in-list vars))
              (val (in-list vals)))
          (define-var var val env*))
        env*)
      (error "arity mismatch:" vars vals)))

(define (define-var var val env)
  (unless (symbol? var)
    (error "not a variable:" var))
  (let ((frame (car env)))
    (if (hash-has-key? frame var)
        (error "already defined:" var)
        (begin (hash-set! frame var val)
               var))))

(define (get-var var env)
  (if (null? env)
      (error "undefined:" var)
      (let ((frame (car env)))
        (if (hash-has-key? frame var)
            (hash-ref frame var)
            (get-var var (cdr env))))))

(define (set-var var val env)
  (if (null? env)
      (error "undefined:" var)
      (let ((frame (car env)))
        (if (hash-has-key? frame var)
            (begin (hash-set! frame var val)
                   val)
            (set-var var val (cdr env))))))
